plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id("maven-publish")
}

android {
    namespace 'com.admobads.ads'
    compileSdk 35

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    lint {
        baseline = file("lint-baseline.xml")
    }
}

ext {
    PUBLISH_GROUP_ID = "com.github.ads"
    PUBLISH_VERSION = "1.1-alpha"
    PUBLISH_ARTIFACT_ID = "admobads"
    PUBLISH_DESCRIPTION = "An android gradle plugin for admob ads"
    PUBLISH_URL = "https://github.com/Technest-PK/Google-Admob-Ads"
    PUBLISH_LICENSE_NAME = "Apache 2.0 License"
    PUBLISH_LICENSE_URL = "https://www.apache.org/licenses/LICENSE-2.0"
    PUBLISH_DEVELOPER_ID = "Technest-PK"
    PUBLISH_DEVELOPER_NAME = "Technest-PK"
    PUBLISH_DEVELOPER_EMAIL = "mailto:developerresource@technest.pk"
    PUBLISH_SCM_CONNECTION = "scm:git:ssh://github.com/Technest-PK/Google-Admob-Ads.git"
    PUBLISH_SCM_DEVELOPER_CONNECTION = "scm:git:ssh://github.com/Technest-PK/Google-Admob-Ads.git"
    PUBLISH_SCM_URL = "https://github.com/Technest-PK/Google-Admob-Ads"
}

// Set version and group using `ext` properties
version = project.ext.PUBLISH_VERSION
group = project.ext.PUBLISH_GROUP_ID

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            create("release", MavenPublication) {
                // Applies the component for the release build variant.
                from components["release"]

                // Use the properties from the `ext` block
                groupId = project.ext.PUBLISH_GROUP_ID
                artifactId = project.ext.PUBLISH_ARTIFACT_ID
                version = project.ext.PUBLISH_VERSION

                // Add additional metadata to the POM
                pom {
                    name.set(project.ext.PUBLISH_ARTIFACT_ID)
                    description.set(project.ext.PUBLISH_DESCRIPTION)
                    url.set(project.ext.PUBLISH_URL)

                    licenses {
                        license {
                            name.set(project.ext.PUBLISH_LICENSE_NAME)
                            url.set(project.ext.PUBLISH_LICENSE_URL)
                        }
                    }

                    developers {
                        developer {
                            id.set(project.ext.PUBLISH_DEVELOPER_ID)
                            name.set(project.ext.PUBLISH_DEVELOPER_NAME)
                            email.set(project.ext.PUBLISH_DEVELOPER_EMAIL)
                        }
                    }

                    scm {
                        connection.set(project.ext.PUBLISH_SCM_CONNECTION)
                        developerConnection.set(project.ext.PUBLISH_SCM_DEVELOPER_CONNECTION)
                        url.set(project.ext.PUBLISH_SCM_URL)
                    }
                }
            }
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    implementation "com.google.android.material:material:1.12.0"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.google.android.gms:play-services-ads:23.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'com.intuit.sdp:sdp-android:1.1.0'

    //skelton
    implementation libs.agskeletonloading
}